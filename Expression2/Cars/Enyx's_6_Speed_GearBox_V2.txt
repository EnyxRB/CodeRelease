@name Enyx's Holo 6 Speed GearBox V2
@inputs Mouse1 Mouse2 PrevWeapon NextWeapon Clutch
@outputs Gear Neutral
@persist MaxGear Gear HoloGridDisplay Sounds Up Across
@persist Mouse1 Mouse2 PrevWeapon NextWeapon

if (dupefinished()) {reset()}

if (first()){

#INSTRUCTIONS
#-First, choose the settings you wish to use with the e2.
#-Wire Gear and Clutch of your acf gearbox / e2 engine / prop engine to the Gear output on the chip, for realistic shifting.
#-Refresh the E2, parent the E2 to the base of the car, and your all done!

#SETTINGS    
GearChangeTimer = 150 #THE TIME TO TRANSITION BETWEEN EACH STEP. MAKE THIS LESS FOR A FASTER CHANGE
HoloGridDisplay = 0 #DISPLAY THE GRID OF THE PATTERN SYSTEM
Sounds = 1 #DISABLE OR ENABLE THE GEAR SHIFT SOUNDS

#------------DO NOT CHANGE ANYTHING UNDER HERE------------#
MaxGear = 6
Gear = 0
Ready = 1  

holoCreate(10) holoCreate(11) holoCreate(12) holoCreate(13) holoCreate(14) holoCreate(15) holoCreate(16)
holoModel(10,"models/sprops/cylinders/size_1/cylinder_1_5x12.mdl") holoModel(11,"models/sprops/cylinders/size_2/cylinder_3x3.mdl") holoModel(12,"hq_icosphere")
holoModel(13, "models/sprops/misc/fittings/cred_3_1_5_tall.mdl") holoModel(14,"models/sprops/rectangles_thin/size_1/rect_3x3x1_5.mdl")
holoModel(15,"models/sprops/rectangles_thin/size_1/rect_3x3x1_5.mdl") holoModel(16,"models/sprops/misc/alphanum/alphanum_1.mdl")
holoMaterial(10,"sprops/textures/sprops_metal5") holoMaterial(11,"phoenix_storms/fender_chrome") holoMaterial(13,"sprops/textures/sprops_metal5") holoMaterial(14,"sprops/textures/sprops_metal5")
holoMaterial(15,"phoenix_storms/fender_white") holoMaterial(16,"phoenix_storms/fender_white")
holoColor(10,vec(40,40,40)) holoColor(11,vec(40)) holoColor(12,vec(50)) holoColor(13,vec(45)) holoColor(14,vec(60)) holoColor(15,vec(25))
holoScale(11,vec(0.65)) holoScale(12,vec(0.5,0.5,0.15)) holoScale(15,vec(0.3,0.5,0.05)) holoScale(16,vec(0.04,0.01,0.04))
holoParent(11,holoEntity(10))
holoParent(10,entity()) holoParent(12,entity()) holoParent(13, holoEntity(10)) holoParent(14, entity()) holoParent(15, entity()) holoParent(16, entity())
holoAng(10,holoEntity(10):angles()-ang(0,holoEntity(10):angles():yaw(),0)) holoAng(11,holoEntity(10):angles()+ang(0,-48,0)) holoAng(13,holoEntity(10):angles()+ang(180,0,0))
holoAng(15,holoEntity(15):angles()+ang(30,0,0)) holoAng(16,entity():angles()+ang(0,-90,60))
holoPos(11,holoEntity(10):pos()+holoEntity(10):up()*-6.5) holoPos(13,holoEntity(13):pos()+holoEntity(13):up()*1) holoPos(15,holoEntity(15):pos()+entity():forward()*2.5+entity():up()*0.5)
holoPos(16,holoEntity(15):pos()+holoEntity(16):right()*-0.07)
holoBodygroup(16,0,1)
holoClipEnabled(10,1,1) holoClipEnabled(13,1,1)
holoClip(10,1,vec(0,0,0),vec(0,0,-1),0) holoClip(13,1,vec(0,0,-1),vec(0,0,1),0)

holoAng(10,entity():toWorld(ang(180+Up,Across,0)))
holoModel(16,"models/sprops/misc/alphanum/alphanum_n.mdl")

entity():setAlpha(0)
}

function gearSound() {
    if (Sounds){
        entity():soundPlay(1,0,"acf_extra/vehiclefx/trans/default_shift.wav")   
        stoptimer("Stopsound") 
        timer("Stopsound",150)
    }
}

function blockSound() {
    if (Sounds){
        entity():soundPlay(2,0,"acf_extra/vehiclefx/trans/default_shift.wav")   
        soundVolume(2,0.7)
        soundPitch(2,40)
        stoptimer("Stopsound")
        timer("Stopsound",150)
    }
}

function noClutchShift() {
    if (Sounds){
        entity():soundPlay(3,0,"physics/metal/metal_box_strain1.wav")    
        soundPitch(3,200)
        soundVolume(3,1)
        stoptimer("Stopsound")
        timer("Stopsound",150)
    }
    
}

function holoAngC() {holoAng(10,entity():toWorld(ang(180+(Up*20),0,Across*20)))}

function updateGear() {

    if (Up==-1 & Across==-1) {Gear=1, Neutral=0, gearSound(), holoModel(16,"models/sprops/misc/alphanum/alphanum_"+Gear+".mdl")}
    elseif (Up==1 & Across==-1) {Gear=2, Neutral=0, gearSound(), holoModel(16,"models/sprops/misc/alphanum/alphanum_"+Gear+".mdl")}
    elseif (Up==-1 & Across==0) {Gear=3, Neutral=0, gearSound(), holoModel(16,"models/sprops/misc/alphanum/alphanum_"+Gear+".mdl")}
    elseif (Up==1 & Across==0) {Gear=4, Neutral=0, gearSound(), holoModel(16,"models/sprops/misc/alphanum/alphanum_"+Gear+".mdl")}
    elseif (Up==-1 & Across==1) {Gear=5, Neutral=0, gearSound(), holoModel(16,"models/sprops/misc/alphanum/alphanum_"+Gear+".mdl")}
    elseif (Up==1 & Across==1) {Gear=6, Neutral=0, gearSound(), holoModel(16,"models/sprops/misc/alphanum/alphanum_"+Gear+".mdl")}
    else {Gear=0, Neutral=1, holoModel(16,"models/sprops/misc/alphanum/alphanum_n.mdl")}
    print(Gear)
}

if (~NextWeapon & NextWeapon){
        if (Up<1){
            if (Clutch){
                Up++
                holoAngC()
                blockSound()
                updateGear()
            }
            else {noClutchShift()}
        }

}
    
if (~PrevWeapon & PrevWeapon){
    if (Up>-1){
        if (Clutch){
            Up--
            holoAngC()
            blockSound()
            updateGear()
        }
        else {noClutchShift()}
    }
}

if (~Mouse2 & Mouse2){
    if (Up==0){
        if (Across<1){
            if (Clutch){
                Across++
                holoAngC()
                blockSound()
                updateGear()
            }
            else {noClutchShift()}
        }
    }
}
    
if (~Mouse1 & Mouse1){
    if (Up==0){
        if (Across>-1){
            if (Clutch){
                Across--
                holoAngC()
                blockSound()
                updateGear()
            }
            else {noClutchShift()}
        }
    }
}

if (clk("Stopsound")) {soundStop(2,0), soundStop(1,0), soundStop(3,0)}
